#lang rhombus/static/and_meta
import:
  "dotting.rhm"
  "point.rhm" open:
    rename: PointLike as _PointLike
  ._PointLike.to_point as PointLike
  "size.rhm" open:
    rename: SizeLike as _SizeLike
  ._SizeLike.to_size as SizeLike

export:
  Rect
  RectLike

class Rect(x :: Real, y :: Real, width :: NonnegReal, height :: NonnegReal):
  constructor
  | (x :: Real, y :: Real, width :: NonnegReal, height :: NonnegReal):
      super(x, y, width, height)
  | (topleft :: PointLike, size :: SizeLike):
      super(topleft.x, topleft.y, size.width, size.height)

  property point :: Point: Point(x, y)
  property size :: Size: Size(width, height)

  export:
    zero

def zero = Rect(0, 0, 0, 0)

dotting.annot_macro 'RectLike' ('to_rect' => 'RectLike.to_rect'):
  'Rect
     || [Real, Real, NonnegReal, NonnegReal]
     || [PointLike, SizeLike]
     || matching({#'x: _ :: Real, #'y: _ :: Real, #'width: _ :: NonnegReal, #'height: _ :: NonnegReal})
     || matching({#'point: _ :: PointLike, #'size: _ :: Size})'

namespace RectLike:
  export:
    to_rect

  annot.macro 'to_rect':
    'Rect
       || converting(fun ([x :: Real, y :: Real, w :: NonnegReal, h :: NonnegReal]) :: Rect: Rect(x, y, w, h))
       || converting(fun ([topleft :: PointLike, size :: SizeLike]) :: Rect: Rect(topleft.x, topleft.y, size.width, size.height))
       || converting(fun ({#'x: x :: Real, #'y: y :: Real, #'width: w :: NonnegReal, #'height: h :: NonnegReal}) :: Rect:
                       Rect(x, y, w, h))
       || converting(fun ({#'point: topleft :: PointLike, #'size: size :: SizeLike}) :: Rect:
                       Rect(topleft.x, topleft.y, size.width, size.height))'

  fun to_rect(r :: to_rect described_as RectLike):
    r
