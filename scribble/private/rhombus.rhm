#lang rhombus
import:
  rhombus/macro open
  "typeset-rhombus.rkt" open
  for_meta:
    "typeset_meta.rhm"
  scribble/base as scribble:
    expose: elem

export:
  rhombus
  rhombusblock

for_meta:
  def | map(proc, []): []
      | map(proc, [a, as, ...]): cons(proc(a), map(proc, as))

  def | andmap(proc, []): #true
      | andmap(proc, [a, as, ...]): proc(a) && andmap(proc, as)

  def | map2(proc, [], []): []
      | map2(proc, [a, as, ...], [b, bs, ...]): cons(proc(a, b), map2(proc, as, bs))

  def head_context(stxs):
    match stxs
    | '$head $_ ...':
        relocate_syntax('#false', head)

  def head_escape_context(stxs):
    match stxs
    | '$head $esc $_ ...':
        relocate_span_syntax('#false', [head, esc])

  def group_context(g):
    relocate_syntax('#false', g)

  def literal_term(stxs):
    'literal_syntax($stxs)'

  def literal_group(stxs):
    'literal_group_syntax($stxs)'

  def is_literal_term(stx):
    match stx
    | 'literal_syntax $_': #true
    | ~else: #false

  def is_literal_group(stx):
    match stx
    | 'literal_group_syntax $_': #true
    | ~else: #false

  def nested(gs, builder, stxs):
    val new_gs: map(escape_group, gs)
    if andmap(is_literal_group, new_gs)
    | literal_term(stxs)
    | '$builder([$new_gs, ...], literal_syntax($(head_context(stxs))))'

  def nested_alts(bs, stxs):
    val new_bs: map(escape_term, bs)
    if andmap(is_literal_term, new_bs)
    | literal_term(stxs)
    | 'alts_syntax([$new_bs, ...], literal_syntax($(head_context(stxs))))'

  def escape_group(g):
    val new_g: escape_tail(g)
    if is_literal_group(new_g)
    | literal_group(g)
    | 'relocate_syntax($new_g, literal_syntax($(group_context(g))))'

  def escape_tail(stxs):
    match stxs
    | '$$($expr ...) $tail ...':
        'sequence_cons_syntax(elem($expr ...), $(escape_tail(tail)),
                              literal_syntax($(head_escape_context(stxs))))'
    | '$head $tail ...':
        val values(a_head, a_tail): adjust_spaces(head, tail)
        val new_head: escape_term('$a_head')
        val new_tail: escape_tail('$a_tail ...')
        if is_literal_term(new_head) && is_literal_group(new_tail)
        | literal_group(stxs)
        | 'sequence_append_syntax($new_head, $new_tail)'
    | ~else: literal_group(stxs)

  def escape_term(stxs):
    match stxs
    | '($(gs :: Group), ...)':
        nested(gs, 'parens_syntax', stxs)
    | '[$(gs :: Group), ...]':
        nested(gs, 'brackets_syntax', stxs)
    | '{$(gs :: Group), ...}':
        nested(gs, 'braces_syntax', stxs)
    | '«'$(gs :: Group); ...'»':
        nested(gs, 'quotes_syntax', stxs)
    | ': $(gs :: Group); ...':
        nested(gs, 'block_syntax', stxs)
    | '| $(bs :: Block) | ...':
        nested_alts(bs, stxs)
    | '$(id :: Id_Op)':
        val mv: syntax_meta_value(typeset_meta.in_space(id), #false)
        match mv
        | typeset_meta.Transformer(proc):
            'relocate_expansion($(proc(id)),
                                literal_syntax($(head_context(stxs))))'
        | ~else: literal_term(stxs)
    | ~else: literal_term(stxs)

  def adjust_spaces(head, tail):
    match '$head'
    | '$(id :: Id_Op)':
        val mv: syntax_meta_value(typeset_meta.in_space(id), #false)
        match mv
        | typeset_meta.Spacer(proc):
            proc(head, tail, '$$')                
        | ~else: values(head, tail)
    | ~else: values(head, tail)

fun sequence_cons_syntax(a, d, context):
  let a_r: relocate_syntax(to_syntax(a), context);
  match d
  | '$e ...': '$a_r $e ...'

fun sequence_append_syntax(d1, d2):
  match d1
  | '$e1 ...':
      match d2
      | '$e2 ...':
          '$e1 ... $e2 ...'
      
fun parens_syntax(ts, context):
  match ts
  | ['$t', ...]: relocate_syntax('($t, ...)', context)

fun brackets_syntax(ts, context):
  match ts
  | ['$t', ...]: relocate_syntax('[$t, ...]', context)

fun braces_syntax(ts, context):
  match ts
  | ['$t', ...]: relocate_syntax('{$t, ...}', context)

fun block_syntax(ts, context):
  match ts
  | ['$t', ...]: relocate_syntax(': $t; ...', context)

fun quotes_syntax(ts, context):
  match ts
  | ['$t', ...]: relocate_syntax('«'$t; ...'»', context)

fun alts_syntax(ts, context):
  relocate_syntax(to_alts_syntax(ts), context)

fun relocate_expansion(e, context):
  relocate_syntax(to_syntax(e), context)

// ----------------------------------------

expr.macro
| 'rhombus ($(forms :: Group)) $main_tail ...':
    values('#{typeset-rhombus}($(escape_group(forms)))',
           main_tail)
| 'rhombus ($forms, $(kw_stx :: Keyw)) $main_tail ...':
    val kw: unwrap_syntax(kw_stx)
    if (kw === keyword(~var)
          || kw === keyword(~bind)
          || kw === keyword(~impmod)
          || kw === keyword(~ann)
          || kw === keyword(~stxclass)
          || kw === keyword(~folder))
    | values('#{typeset-rhombus}(~space: keyword($kw), $(escape_group(forms)))',
             main_tail)
    | raise_syntax_error("invalid space", kw)

expr.macro 'rhombusblock $tail ...':
  ~op_stx: me
  fun finish(option, fin_tail):
    match '$fin_tail ...'
    | ': $_':
        values('#{typeset-rhombusblock}($option, ..., $(escape_term('$fin_tail ...')))', '')
    | ~else: raise_syntax_error("expected a block", '$me $tail ...')
  fun check_options(options):
    match '($options, ...)'
    | '()': #true
    | '(~inset: $on, $more, ...)': check_options(more)
    | '(~indent: $amt, $more, ...)': check_options(more)
    | '(~prompt: $prompt, $more, ...)': check_options(more)
    | '($opt, $_, ...)': raise_syntax_error("invalid option", opt)
  match '$tail ...'
  | '($options, ...) $new_tail ...':
      check_options(options)
      finish(options, new_tail)
  | ~else: finish([], tail)
