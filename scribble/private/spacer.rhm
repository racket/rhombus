#lang rhombus/static/and_meta
import:
  "typeset_meta.rhm"
  "escape.rhm".Escape
  meta:
    "add-space.rkt".#{full-space-names}

export:
  meta:
    adjust_group
    adjust_term
    adjust_sequence
    adjust_multi
    rename:
      set_space as set
  bridge
  typeset_meta.typeset
    
meta:
  fun set_space(stx, context):
    let context = if context is_a Keyword | Symbol.from_string(to_string(context)) | context
    match stx
    | '$(_ :: Term)':
        Syntax.property(stx, #'#{typeset-space-name}, context, #true)
    | '$(t :: Term) ...':
        '$(Syntax.property(t, #'#{typeset-space-name}, context, #true)) ...'.relocate(stx)

  fun find_relevant_context(context, in_contexts :~ List):
    let nows :~ List = #{full-space-names}(context)
    // `relevant_context` can be `#false` (the default space)
    for values(found = #false, relevant_context = #false):
      each now: nows
      each in: in_contexts
      skip_when now != in
      final_when #true
      values(#true, now)

  fun adjust_term(t :: Term, context, esc) :~ Syntax:
    match t
    | '($(g :: Group), ...)':
        '($(adjust_group(g, context, esc)), ...)'.relocate(t)
    | '[$(g :: Group), ...]':
        '[$(adjust_group(g, context, esc)), ...]'.relocate(t)
    | '{$(g :: Group), ...}':
        '{$(adjust_group(g, context, esc)), ...}'.relocate(t)
    | '«'$(g:: Group); ...'»':
        '«'$(adjust_group(g, context, esc)); ...'»'.relocate(t)
    | ': $_':
        adjust_block(t, context, esc)
    | '| $(b :: Block) | ...':
        Syntax.make([#'alts, adjust_block(b, context, esc), ...]).relocate(t)
    | '$(_ :: Name)':
        set_space(t, context)
    | ~else:
        t

  fun adjust_block(b :: Block, context, esc) :~ Syntax:
    match b
    | ': $g; ...':
        ': $(adjust_group(g, context, esc)); ...'.relocate(b)

  fun adjust_group(g :: Group, context, esc) :~ Syntax:
     match adjust_sequence(g, context, esc)
     | '$(new_g :: Group)': new_g.relocate(g)
     | '': error("group turned into empty sequence: " +& to_string(g, ~mode: #'expr))

  fun adjust_sequence(seq :: TermSequence, context, esc) :~ Syntax:
    fun loop(head, tail):
      '$head $(adjust_sequence(tail, context, esc))'
    match seq
    | '$(id :: Name) $tail ...':
        fun fail():
          loop(set_space(id, context), '$tail ...')
        let mv = syntax_meta.value(id, typeset_meta.space, #false)
        match mv
        | typeset_meta.Spacer(in_contexts, proc):
            let (found, relevant_context) = find_relevant_context(context, in_contexts)
            if found
            | let res:
                try:
                  proc(set_space(id, relevant_context), '$tail ...', context, esc)
                  ~catch x :: Exn.Fail:
                    error("error from " +& to_string(id, ~mode: #'expr) +& " spacer\n"
                            +& "  message: " +& Exn.message(x))
              match res
              | '$_ ...': res
              | ~else:
                  error(#false, "spacer " +& proc +& " returned wrong value " +& to_string(res, ~mode: #'expr))
            | fail()
        | ~else:
            fail()
    | '$(escape :: Escape(esc)) $tail ...':
        loop(escape, '$tail ...')
    | '$head $tail ...':
        loop(adjust_term(head, context, esc), '$tail ...')
    | ~else:
        seq

  fun adjust_multi(m, context, esc) :~ Syntax:
    match m
    | '$g; ...':
        '$(adjust_group(g, context, esc)); ...'

defn.macro 'bridge $(name :: Name)($(self :: Identifier),
                                   $(tail :: Identifier),
                                   $(mode :: Identifier),
                                   $(esc :: Identifier)):
                   ~in: $space ...
                   $body
                   ...':
  let [space_name, ...] = List.append(#{full-space-names}(space.unwrap()), ...)
  fun quote(s): if s | '#' $s' | '#false'
  'meta.bridge $(typeset_meta.in_space(name)):
     typeset_meta.Spacer(
       [$(quote(space_name)), ...],
       fun ($self :: Syntax, $tail :: Syntax, $mode, $esc :: Syntax):
         $body
         ...
     )'
