#lang rhombus/static/and_meta
import:
  lib("racket/gui/easy.rkt")
  lib("racket/gui/base.rkt") as gui
  rhombus/draw/private/symbol_map.symbol_map_annot
  rhombus/draw/private/rkt

export:
  MouseEvent
  KeyEvent

module private:
  export:
    _MouseEvent
    _KeyEvent
    convert_key

meta fun make_message(key):
  Syntax.make_id("get-" +& key +& "-down", key)

expr.macro '$left get_all_down $handle $('. $key') ...':
  'block:
     let s = $left
     let s = (if rkt.send $handle . $(make_message(key))()
              | (s :~ Set) ++ { #'$key }
              | s)
     ...
     s'

class KeyMouseEvent(private _handle):
  nonfinal
  property handle: _handle

  constructor (hand):
    super(hand)

  // result is cached in subclasses:
  property downs:
    Set{} get_all_down handle
      .shift
      .control
      .meta
      .alt
      .caps
      .mod3
      .mod4
      .mod5

  // `sym` is restricted to appropriate values in subclasses:
  method is_down(sym):
    expr.macro 'match_down | $key | ...':
      'match sym
       | #'$key: rkt.send handle . $(make_message(key))()
       | ...
       | ~else: error("bad symbol")'
    match_down
    | left
    | middle
    | right
    | shift
    | control
    | meta
    | alt
    | caps
    | mod3
    | mod4
    | mod5

  property x: rkt.send handle.#{get-x}()
  property y: rkt.send handle.#{get-y}()
  property timestamp: rkt.send handle.#{get-time-stamp}()


class MouseEvent():
  extends KeyMouseEvent
  internal _MouseEvent
  private field down_set = #false

  constructor (~kind: kind :: MouseEvent.Kind = #'motion,
               ~downs: down :: Set.of(MouseEvent.Down) = Set{},
               ~x: x :: Int = 0,
               ~y: y :: Int = 0,
               ~timestamp: timestamp :: Int = 0):
    super(rkt.make_object(gui.#{mouse-event%},
                          convert_kind(kind),
                          #'left in down, #'middle in down, #'right in down,
                          x, y,
                          #'shift in down, #'control in down, #'meta in down, #'alt in down,
                          timestamp,
                          #'caps in down, #'mod3 in down, #'mod4 in down, #'mod5 in down))()

  property kind:
    unconvert_kind(rkt.send handle.#{get-event-type}())

  override property downs :: Set:
    down_set || (block:
                   let s:
                     super.downs get_all_down handle
                       .left
                       .middle
                       .right
                   down_set := s
                   s)

  override method is_down(sym :: MouseEvent.Down):
    super.is_down(sym)

  method is_button_changed(which :: MouseEvent.Button = #'any):
    rkt.send handle.#{button-changed?}(which)
  method is_button_press(which :: MouseEvent.Button = #'any):
    rkt.send handle.#{button-down?}(which)
  method is_button_release(which :: MouseEvent.Button = #'any):
    rkt.send handle.#{button-down?}(which)
  method is_dragging():
    rkt.send handle.#{dragging?}()

  export:
    Kind
    Down
    Button

  symbol_map_annot Kind convert_kind unconvert_kind
  | enter = enter
  | leave = leave
  | left_down = #{left-down}
  | left_up = #{left-up}
  | middle_down = #{middle-down}
  | middle_up = #{middle-up}
  | right_down = #{right-down}
  | right_up = #{right-up}
  | motion

  enum Down
  | left
  | middle
  | right
  | shift | control | meta | alt
  | caps | mod3 | mod4 | mod5

  enum Button
  | left | middle | right | any

class KeyEvent():
  extends KeyMouseEvent
  internal _KeyEvent
  private field down_set = #false

  constructor (code :: (Char || KeyEvent.Key),
               ~release_code: release_code :: (Char || KeyEvent.Key) = #'press,
               ~other_caps_code: other_caps_code :: maybe(Char || KeyEvent.Key) = #false,
               ~other_shift_code: other_shift_code :: maybe(Char || KeyEvent.Key) = #false,
               ~other_altgr_code: other_altgr_code :: maybe(Char || KeyEvent.Key) = #false,
               ~other_shift_altgr_code: other_shift_altgr_code :: maybe(Char || KeyEvent.Key) = #false,
               ~downs: down :: Set.of(KeyEvent.Down) = Set{},
               ~x: x :: Int = 0,
               ~y: y :: Int = 0,
               ~timestamp: timestamp :: Int = 0,
               ~use_altgr: use_altgr = #true):
    let evt = rkt.make_object(gui.#{key-event%},
                              match code
                              | _ :: Char: code
                              | ~else: convert_key(code),
                              #'shift in down, #'control in down, #'meta in down, #'alt in down,
                              x, y,
                              timestamp,
                              #'caps in down, #'mod3 in down, #'mod4 in down, #'mod5 in down,
                              use_altgr && #true)
    rkt.send evt.#{set-key-release-code}(release_code)
    when other_caps_code | rkt.send evt.#{set-other-caps-key-code}(other_caps_code)
    when other_shift_code | rkt.send evt.#{set-other-shift-key-code}(other_shift_code)
    when other_altgr_code | rkt.send evt.#{set-other-altgr-key-code}(other_altgr_code)
    when other_shift_altgr_code | rkt.send evt.#{set-other-shift-altgr-key-code}(other_shift_altgr_code)
    super(evt)()

  override property downs :: Set:
    down_set || (block:
                   let s = super.downs
                   down_set := s
                   s)

  override method is_down(sym :: KeyEvent.Down):
    super.is_down(sym)

  property code: rkt.send handle.#{get-key-code}()
  property release_code: rkt.send handle.#{get-key-release-code}()
  property other_altgr_code: rkt.send handle.#{get-other-altgr-key-code}()
  property other_caps_code: rkt.send handle.#{get-other-caps-key-code}()
  property other_shift_code: rkt.send handle.#{get-other-shift-key-code}()
  property other_shift_altgr_code: rkt.send handle.#{get-other-shift-altgr-key-code}()

  property wheel_steps: rkt.send handle.#{get-wheel-steps}()

  export:
    Down
    Key

  enum Down
  | shift | control | meta | alt
  | caps | mod3 | mod4 | mod5

symbol_map_annot Key convert_key
| start
| cancel
| clear
| shift
| rshift
| control
| rcontrol
| menu
| pause
| capital
| prior
| next
| end
| home
| left
| up
| right
| down
| escape
| select
| print
| execute
| snapshot
| insert
| help
| numpad0
| numpad1
| numpad2
| numpad3
| numpad4
| numpad5
| numpad6
| numpad7
| numpad8
| numpad9
| numpad_enter = #{numpad-enter}
| multiply
| add
| separator
| subtract
| decimal
| divide
| f1
| f2
| f3
| f4
| f5
| f6
| f7
| f8
| f9
| f10
| f11
| f12
| f13
| f14
| f15
| f16
| f17
| f18
| f19
| f20
| f21
| f22
| f23
| f24
| numlock
| scroll
| wheel_up = #{wheel-up}
| wheel_down = #{wheel-down}
| wheel_left = #{wheel-left}
| wheel_right = #{wheel-right}
| release
| press
