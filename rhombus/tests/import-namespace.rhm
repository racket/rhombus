#lang rhombus/and_meta

namespace n:
  export: exint
  def exint = 10
  bind.only.macro 'exint': '(_ :: Integer)'
  annot.only.macro 'exint': 'Integer'

check:
  import: .n as ex_c
  def ex_c.exint = 10
  10 :: ex_c.exint
  ex_c.exint
  ~is 10

check:
  def n.exstr = "11"
  annot.only.macro '(n.exstr)': 'String'
  import: .n as ex_c
  def ex_c.exint = 10
  [10 :: ex_c.exint,
   ex_c.exint,
   "11" :: ex_c.exstr,
   ex_c.exstr]
  ~is [10, 10, "11", "11"]

check:
  import: .n as ex_c
  def ex_c.exint = "10"
  ~raises "value does not satisfy annotation"

check:
  import: .n as ex_c
  "10" :: ex_c.exint
  ~raises "value does not satisfy annotation"

check:
  import: .n open
  def exint = 10
  10 :: exint
  exint
  ~is 10

check:
  import:
    .n:
      open
      rename: exint as ex_int
  def ex_int = 10
  10 :: ex_int
  ex_int
  ~is 10

check:
  import:
    .n as ex_c:
      rename: exint as ex_int
  def ex_c.ex_int = 10
  10 :: ex_c.ex_int
  ex_c.ex_int
  ~is 10

check:
  import:
    .n:
      open
      only_space: ~default
  exint
  ~is 10

check:
  import:
    .n as ex_c:
      only_space: ~default
  ex_c.exint
  ~is 10

check:
  ~eval
  begin:
    import rhombus/meta open
    namespace n:
      export: exint
      def exint = 10
      bind.only.macro 'exint': '(_ :: Integer)'
      annot.only.macro 'exint': 'Integer'
    import:
      .n:
        open
        only_space: ~default
    10 :: exint
  ~raises "not bound as an annotation"

check:
  ~eval
  begin:
    import rhombus/meta open
    namespace n:
      export: exint
      def exint = 10
      bind.only.macro 'exint': '(_ :: Integer)'
      annot.only.macro 'exint': 'Integer'
    import:
      .n as ex_c:
        only_space: ~default
    10 :: ex_c.exint
  ~raises "not bound as an annotation"

check:
  import:
    .n:
      open
      only_space: annot
  def exint = 11
  exint
  ~is 11

check:
  import:
    .n:
      open
      only_space: annot
  11 :: exint
  ~is 11

check:
  import:
    .n as ex_c:
      only_space: annot
  11 :: ex_c.exint
  ~is 11

begin:
  namespace n2:
    import:
      .n open:
        except_space: bind
    export: exint
    bind.only.macro 'exint': '(_ :: PositiveInteger)'                            
  check:
    import .n2 as ex_c
    def ex_c.exint = 10
    10 :: ex_c.exint
    ex_c.exint
    ~is 10
  check:
    import .n as ex_c
    def ex_c.exint = -10
    ~completes
  check:
    import .n2 as ex_c
    def ex_c.exint = -10
    ~raises "value does not satisfy annotation"
  check:
    import .n2 open
    def exint = -10
    ~raises "value does not satisfy annotation"
