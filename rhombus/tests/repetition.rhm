#lang rhombus

// syntax-object templates with repetitions
block:
  def [x, ...] = [1, 2, 3]
  def [[y, ...], ...] = [["a", "b", "c"], ["A", "B", "C"]]
  def [[z, ...], _] = [["a", "b", "c"], ["A", "B", "C"]]
  def [_, &[q]] = [["a", "b", "c"], ["A", "B", "C"]]
  def [_, &[w, ...]] = [["a", "b", "c"], ["A", "B", "C"]]

  check:
    [x, ...]
    ~is [1, 2, 3]
  check:
    '[($y) ...] ...'
    ~prints_like '[("a") ("b") ("c")] [("A") ("B") ("C")]'
  check:
    '[($z) ...]'
    ~prints_like '[("a") ("b") ("c")]'
  check:
    q
    ~is ["A", "B", "C"]
  check:
    [w, ...]
    ~is [["A", "B", "C"]]

// syntax objects as repetitions
block:
  def [x, ...] = ['1', '2', '3']
  check:
    ['$x+1', ...]
    ~prints_like ['1+1', '2+1', '3+1']

// operator uses as reptitions
block:
  def [x, ...] = [1, 4, 3]
  def four = 4
  check:
    [!(x == four), ...]
    ~is [#true, #false, #true]
  check:
    [(x + (- four)), ...]
    ~is [-3, 0, -1]
  check:
    operator (x %% y): [x, y]
    [x %% four, ...]
    ~is [[1, 4], [4, 4], [3, 4]]

// function calls as reptitions
block:
  def [x, ...] = [1, 2, 3]
  check:
    fun f(x, y): {x: y}
    [f(x, 0), ...]
    ~is [{1: 0}, {2: 0}, {3: 0}]    
  check:
    fun f(~x: x, y): {x: y}
    [f(~x: x, 0), ...]
    ~is [{1: 0}, {2: 0}, {3: 0}]
  check:
    fun f(& vs): vs
    [f(x, x, ...), ...]
    ~is [[1, 1, 2, 3], [2, 1, 2, 3], [3, 1, 2, 3]]

// function calls with rest arguments as repetitions
block:
  def [x, ...] = [[1], [2, 3]]
  check:
    fun f(& vs): vs
    [f(&x), ...]
    ~is [[1], [2, 3]]

// function calls with repetition arguments as repetitions
block:
  def [[x, ...], ...] = [[1, 2, 3], [4, 5]]
  check:
    fun f(v, ...): [[v, ...]]
    [f(x, ...), ...]
    ~is [[[1, 2, 3]], [[4, 5]]]
  check:
    fun f(v, ...): [v, ...]
    [f(x+1, ...), ...]
    ~is [[2, 3, 4], [5, 6]]

// function calls with keyword arguments as repetitions
block:
  def [x, ...] = [{#'~a: "a"}, {#'~b: "b"}]
  fun f(~a: a = #false, ~b: b = #false): [a, b]
  check:
    [f(~& x), ...]
    ~is [["a", #false], [#false, "b"]]

// static information through a constructor call as repetition
check:
  use_static
  class Posn(x, y)
  def [x, ...] = [1, 2, 3]
  [Posn(x, x), ...][1].x
  ~is 2

// static information through nested repetitions
block:
  use_static
  class Posn(x, y)
  def [y :~ Posn, ...] = [Posn(1, 2)]
  def [[x :~ Posn, ...], ...] = [[Posn(1, 2)]]
  def '($z, ...) ...' = '(1, 2, 3) (4, 5)'
  check:
    [y, ...][0].x
    ~is 1
  check:
    [[x, ...], ...][0][0].x
    ~is 1
  check:
    [x, ..., ...][0].x
    ~is 1
  check:
    [[z, ...], ...][1][1].unwrap()
    ~is 5

// map- and set-building `{}` as a repetition
block:
  def [x, ...] = [1, 2, 3]
  check:
    [{x: 1}, ...]
    ~is [{1: 1}, {2: 1}, {3: 1}]
  check:
    [Map{x: 1}, ...]
    ~is [{1: 1}, {2: 1}, {3: 1}]
  check:
    [MutableMap{x: 1}, ...]
    ~is_now [{1: 1}, {2: 1}, {3: 1}].map(fun (s): Map.copy(s))
  check:
    use_static
    def m = [{x: 1}, ...]
    m[1][2]
    ~is 1
  check:
    [Set(x, 1), ...]
    ~is [{1}, {1, 2}, {1, 3}]
  check:
    [MutableSet{x, 1}, ...]
    ~is_now [{1}, {1, 2}, {1, 3}].map(fun (s): Set.copy(s))
  check:
    [MutableSet(x, 1), ...]
    ~is_now [{1}, {1, 2}, {1, 3}].map(fun (s): Set.copy(s))
  check:
    use_static
    def m = [{x}, ...]
    m[1][2]
    ~is #true

// list-builiding `[]` as a repetition
block:
  def [x, ...] = [0, 10, 100]
  check:
    [1, 2, &[3, 4], 5, x, ..., 6]
    ~is [1, 2, 3, 4, 5, 0, 10, 100, 6]
  check:
    [[-1, x], ...]
    ~is [[-1, 0], [-1, 10], [-1, 100]]
  check:
    def [[z, ...], ...] = [[0, 10, 100], [333]]
    [[(z), ...], ...]
    ~is [[(0), (10), 100], [(333)]]

// reference `[]` as a repetition
block:
  use_static
  class Posn(x, y)
  def a :: List.of(Posn):
    [Posn(1, 1), Posn(2, 2), Posn(3, 3), Posn(4, 4)]
  def [x, ...] = [3, 2, 1]
  check:
    [a[x], ...][0].x
    ~is 4

// dotted names as repetitions
block:
  def [i, ...] = [1, 2, 3]
  check:
    [[List.length, i], ...]
    ~is [[List.length, 1], [List.length, 2], [List.length, 3]]
  check:
    namespace M:
      export: N
      namespace N:
        export: three
        def three = 3
    [[i, M.N.three], ...]
    ~is [[1, 3], [2, 3], [3, 3]]
  check:
    namespace M:
      export pi_ish
      def three = 3
      macro 'pi_ish': 'three'
    [[i, M.pi_ish], ...]
    ~is [[1, 3], [2, 3], [3, 3]]

// `.` as a repetition
block:
  use_static
  class Posn(x, y)
  def [p :: Posn, ...] = [Posn(1, 1), Posn(2, 2), Posn(3, 3), Posn(4, 4)]
  check:
    [p.x, ...]
    ~is [1, 2, 3, 4]
  class Square(tl :: Posn, s :: Int)
  def [s :: Square, ...] = [Square(Posn(0, 0), 10)]
  check:
    [s.tl, ...][0].x
    ~is 0

// splicing
block:
  use_static
  class Posn(x, y)
  def [[x :~ Posn, ...], ...] = [[Posn(1, 2)], [Posn(3, 4), Posn(5, 6)]]
  check:
    [x, ..., ...]
    ~is [Posn(1, 2), Posn(3, 4), Posn(5, 6)]
  check:
    [x.x, ..., ...]
    ~is [1, 3, 5]
  check:
    [x, ..., ...][1].x
    ~is 3
  fun f(x, ...): [x, ...]
  check:
    f(x, ..., ...)
    ~is [Posn(1, 2), Posn(3, 4), Posn(5, 6)]
  check:
    f(x, ..., ..., "done")
    ~is [Posn(1, 2), Posn(3, 4), Posn(5, 6), "done"]
  check:
    {x, ..., ...}
    ~is {Posn(1, 2), Posn(3, 4), Posn(5, 6)}
  check:
    {x, ..., ..., 0}
    ~is {0, Posn(1, 2), Posn(3, 4), Posn(5, 6)}
  check:
    {x: #true, ..., ...}
    ~is {Posn(1, 2): #true, Posn(3, 4): #true, Posn(5, 6): #true}
  check:
    {x: #true, ..., ..., 0: 0}
    ~is {0: 0, Posn(1, 2): #true, Posn(3, 4): #true, Posn(5, 6): #true}
  check:
    '$(x.y) ... ...'
    ~prints_like '2 4 6'

// make sure that repetitions work with `let`
check:
  let [index, ...]: [1, 2, 3]
  [index, ...]
  ~is [1, 2, 3]


