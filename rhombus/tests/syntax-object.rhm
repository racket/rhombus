#lang rhombus/and_meta

block:
  import "static_arity.rhm"
  static_arity.check:
    Syntax.make(t, [ctx])
    Syntax.make_group(t, [ctx])
    Syntax.make_sequence(t, [ctx])
    Syntax.unwrap(stx)
    Syntax.unwrap_sequence(stx)

check:
  Syntax.make(1)
  ~prints_like '1'

check:
  Syntax.make([1, 2])
  ~raises "invalid as a shrubbery term"

check:
  Syntax.make([#'parens, [#'group, 1, "a", #'z]])
  ~prints_like '(1 "a" z)'

check:
  Syntax.make([#'parens,
               [#'group, 1],
               [#'group, "a"],
               [#'group, [#'op, #'#{+}]]])
  ~prints_like '(1, "a", +)'

check:
  Syntax.make([#'parens, '1', '"a"', '+'])
  ~prints_like '(1, "a", +)'

check:
  Syntax.make([#'parens, '1 2 3', '"a"', '4 + 5'])
  ~prints_like '(1 2 3, "a", 4 + 5)'

check:
  Syntax.make(['block', '1 2', '3'])
  ~prints_like ': 1 2; 3'

check:
  Syntax.make_group([1, 2])
  ~prints_like '1 2'

check:
  Syntax.make_group(['block', ['block', '1 2', '3']])
  ~prints_like 'block: 1 2; 3'

check:
  Syntax.make(['block'])
  ~prints_like ':«»'

check:
  Syntax.make_group(['block', ['block', '1 2', '3'], 'more'])
  ~raises "invalid as a shrubbery non-tail term"

check:
  Syntax.make_group([])
  ~raises "NonemptyList"

check:
  Syntax.make([#'parens, Syntax.make_group([1])])
  ~prints_like '(1)'

check:
  Syntax.make_sequence(['a', 'b c', 'd e f'])
  ~prints_like 'a; b c; d e f'

check:
  Syntax.unwrap('x')
  ~is #'x

check:
  Syntax.unwrap('(1 2 3)')
  ~prints_like ['parens', '1 2 3']

check:
  Syntax.unwrap('(1, 2, 3)')
  ~prints_like ['parens', '1', '2', '3']

check:
  Syntax.unwrap_sequence('1 2; 3 4 5')
  ~prints_like ['1 2', '3 4 5']

check:
  Syntax.unwrap_sequence('1 2')
  ~prints_like ['1 2']

check:
  Syntax.unwrap_sequence('1')
  ~prints_like ['1']

check:
  match 'a: *'
  | 'a: $b': b is_a Operator
  ~is #true

check:
  match 'a: b'
  | 'a: $b': b is_a Identifier
  ~is #true

check:
  ~eval
  module m ~lang rhombus/and_meta:
    meta:
      import: rhombus.check
      check:
        syntax_meta.equal_binding('check', 'check') ~is #true
        syntax_meta.equal_binding('check', 'def') ~is #false
        syntax_meta.equal_binding('check', 'check', expr_meta.space, 0, 2) ~is #false
        syntax_meta.equal_name_and_scopes('check', 'check') ~is #true
        syntax_meta.equal_name_and_scopes('check', 'check', 0) ~is #true
        syntax_meta.expanding_phase() ~is 0
  ~completes

check:
  ~eval
  module m ~lang rhombus/and_meta:
    meta:
      import: rhombus.check
      namespace ns1:
        import rhombus.Pair.cons
        export x
        def x = 'cons'
      namespace ns2:
        import rhombus.List.cons
        export x
        def x = 'cons'
      namespace ns3:
        import rhombus.List.cons
        export x
        def x = 'cons'
      check:
        syntax_meta.equal_binding(ns1.x, ns2.x, expr_meta.space, 1) ~is #false
        syntax_meta.equal_binding(ns3.x, ns2.x, expr_meta.space, 1) ~is #true
        syntax_meta.equal_name_and_scopes(ns1.x, ns2.x, 1) ~is #false
        syntax_meta.equal_name_and_scopes(ns3.x, ns2.x, 1) ~is #false
      check:
        syntax_meta.equal_binding('+', '+') ~is #true
        syntax_meta.equal_binding('*', '+') ~is #false
        syntax_meta.equal_binding('List.cons', 'List.cons') ~is #true
        syntax_meta.equal_binding('List.cons', 'Pair.cons') ~is #false
        syntax_meta.equal_binding('List.cons', 'List.first') ~is #false
        syntax_meta.equal_binding('List.(+)', 'List.(+)') ~is #true
        syntax_meta.equal_binding('List.(+)', 'Pair.(+)') ~is #false
        syntax_meta.equal_binding('List.(+)', '+') ~is #false
  ~completes

check:
  ~eval
  module m ~lang rhombus/and_meta:
    import: rhombus.check
    expr.macro 'with_y: $expr':
      ~op_stx h
      def y: 'y'.replace_scopes('$h')
      'block:
         def $y: 2
         $expr'
    check: (with_y: y) ~is 2
  ~completes
