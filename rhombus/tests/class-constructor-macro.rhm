#lang rhombus

use_static

check:
  class Posn(x, y):
    constructor Psn(y, x): super(x, y)
    export: Psn
  [Posn.Psn(2, 1).x,
   Posn.Psn(2, 1).y]
  ~is [1, 2]

check:
  class Posn(x, y):
    internal _Posn
    expression 'Posn[$y, $x]': '_Posn($x, $y)'
  [Posn[2, 1].x,
   Posn[2, 1].y]
  ~is [1, 2]

check:
  class Posn(x, y):
    internal _Posn
    constructor Psn(y, x): super(x, y)
    expression 'Posn[$x, &, $y]': '_Posn($x, $y)'
    export: Psn
  Function.map(fun (p :~ Posn): [p.x, p.y],
               [Posn[1, &, 2],
                Posn.Psn(2, 1),
                _Posn(1, 2)])
  ~is [[1, 2],
       [1, 2],
       [1, 2]]

check:
  ~eval
  class Posn(x, y):
    constructor (x): 0
    expression 'Posn': '0'
  ~raises "unnamed constructor inaccessible due to expression macro"
  
check:
  ~eval
  class Posn(x, y):
    constructor Posn(x): 0
    expression 'Posn': '0'
  ~raises "constructor name conflicts with expression macro"

check:
  interface Shape:
    expression 'Shape{}': '"circle"'
  Shape{}
  ~is "circle"

check:
  class Posn(x, y):
    constructor Posn(y, x): super(x, y)
  [Posn(20, 10).x,
   Posn(20, 10).y]
  ~is [10, 20]

block:
  interface Stringish:
    internal _Stringish
    annotation 'Stringish': 'String || _Stringish'
  check:
    "hello" :: Stringish
    ~is "hello"
  check:
    5 :: Stringish
    ~raises "value does not satisfy annotation"
  block:
    class S():
      implements Stringish
    check:
      S() :: Stringish
      ~is S()
