#lang rhombus

use_static

// basic result annotation checks
begin:
  class Posn(x, y):
    nonfinal
    method m() :: Integer:
      x+y
    method n() :: String:
      x+y
    final method n_f() :: String:
      x+y
    // annotation not checked
    method p() -: String:
      x+y
  check:
    Posn(1, 2).m()
    ~is 3
  check:
    Posn(1, 2).n()
    ~raises "result does not satisfy annotation"
  check:
    Posn(1, 2).n_f()
    ~raises "result does not satisfy annotation"
  check:
    Posn(1, 2).p()
    ~is 3

  begin:
    class Posn3D(z):
      extends Posn
      override m():
        "oops"   
      override n():
        "ok"
      override p():
        0 
    check:
      Posn3D(1, 2, 3).m()
      ~raises "result does not satisfy annotation"
    check:
      Posn3D(1, 2, 3).p()
      ~is 0
    check:
      Posn3D(1, 2, 3).n()
      ~is "ok"

// annotation obligations are inherited and compose
begin:
  interface Red
  interface Blue
  interface Green
  class Top():
    nonfinal
    method m(v) :: Red: v
  class Middle():
    nonfinal
    extends Top
    override m(v) :: Blue: v
  class Bottom():
    nonfinal
    extends Middle
    override m(v) :: Green: v

  class White():
    implements: Red Blue Green
  class Magenta():
    implements: Red Blue
  class Cyan():
    implements: Blue Green

  check:
    Top().m(White())
    ~is White()
  check:
    Middle().m(White())
    ~is White()
  check:
    Bottom().m(White())
    ~is White()

  check:
    Top().m(Magenta())
    ~is Magenta()
  check:
    Middle().m(Magenta())
    ~is Magenta()
  check:
    Bottom().m(Magenta())
    ~raises "result does not satisfy annotation"

  check:
    Top().m(Cyan())
    ~raises "result does not satisfy annotation"
  check:
    Middle().m(Cyan())
    ~raises "result does not satisfy annotation"
  check:
    Bottom().m(Cyan())
    ~raises "result does not satisfy annotation"

// class self-reference
begin:
  class Posn(x, y):
    nonfinal
    method zero() :: Posn:
      Posn(0, 0)
  check:
    Posn(1, 2).zero().x
    ~is 0

// class self-reference, final method
begin:
  class Posn(x, y):
    final method zero() :: Posn:
      Posn(0, 0)
  check:
    Posn(1, 2).zero().x
    ~is 0

// class self-reference, private method
begin:
  class Posn(x, y):
    nonfinal
    internal _Posn
    private method zero() :: Posn:
      Posn(0, 0)
  check:
    _Posn(1, 2).zero().x
    ~is 0
  check:
    (Posn(1, 2) -: _Posn).zero().x
    ~is 0

// interface
begin:
  interface Pointy:
    method visible() :: Boolean
    method invisible() :: Boolean:
      "nope"
    method really_invisible() :: Boolean:
      "nope"
  class Point(x, y):
    implements Pointy
    override visible(~as_text = #true):
      if as_text | "yes" | #true
    override really_invisible() :: Boolean:
      #false
  check:
    Point(1, 2).visible(~as_text: #false)
    ~is #true
  check:
    Point(1, 2).visible()
    ~raises "result does not satisfy annotation"
  check:
    Point(1, 2).invisible()
    ~raises "result does not satisfy annotation"
  check:
    Point(1, 2).really_invisible()
    ~is #false

// multiple interface inheritance
begin:
  interface Red:
    method example(x) :: Red:
      this
  interface Blue:
    method example(x) :: Blue:
      this

  class Apple():
    implements Red
  class Sky():
    implements Blue
  class RacketLogo():
    implements Red Blue
    override example(x = this):
      x

  check:
    Apple().example(#false)
    ~is Apple()    
  check:
    Sky().example(#false)
    ~is Sky()    
  check:
    RacketLogo().example()
    ~is RacketLogo()    
  check:
    RacketLogo().example(#false)
    ~raises "result does not satisfy annotation"

// alternate method forms, internal versus external annotations
begin:
  class Top():
    nonfinal
    method m:
      fun (x) :: Integer:
        x
    method m2 :: Integer:
      fun (x):
        x
  class Bottom():
    extends Top
    override m (x):
      x
    override m2 (x):
      x
  check:
    [Top().m(1), Top().m2(2)]
    ~is [1, 2]
  check:
    Top().m("apple")
    ~raises "result does not satisfy annotation"
  check:
    Bottom().m("apple")
    ~is "apple"
  check:
    Bottom().m2("apple")
    ~raises "result does not satisfy annotation"

// abstract override
begin:
  interface Left
  interface Right
  class Both():
    implements: Left Right    
  class LeftC():
    implements: Left
  class RightC():
    implements: Right

  interface A:
    method m() :: Left
  interface B:
    extends A
    abstract override m() :: Right
  interface B2:
    extends A
    override m() :: Right

  class C(x):
    implements: B B2
    override m():
      x

  check:
    C(Both()).m()
    ~is Both()
  check:
    C(#false).m()
    ~raises "result does not satisfy annotation"
  check:
    C(LeftC()).m()
    ~raises "result does not satisfy annotation"
  check:
    C(RightC()).m()
    ~raises "result does not satisfy annotation"
